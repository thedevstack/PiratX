// Top-level build file where you can add configuration options common to all
// sub-projects/modules.
buildscript {
    repositories {
        google()
        mavenCentral()
    }
    dependencies {
        classpath 'com.android.tools.build:gradle:7.0.3'
    }
}

apply plugin: 'com.android.application'

allprojects {
    repositories {
        google()
        mavenCentral()
        maven { url "https://jitpack.io" }
        maven { url "https://raw.github.com/abdularis/libwebrtc-android/repo/" }
    }
}

repositories {
    google()
    mavenCentral()
    maven { url "https://jitpack.io" }
    maven { url "https://raw.github.com/abdularis/libwebrtc-android/repo/" }
    jcenter()
}

configurations {
    playstoreImplementation
    gitImplementation
    implementation.exclude group: 'org.jetbrains' , module:'annotations'
}

dependencies {
    implementation 'org.jitsi:org.otr4j:0.23'
    implementation 'com.github.webrtc-sdk:android:93.4577.01'
    playstoreImplementation('com.google.firebase:firebase-messaging:22.0.0') {
        exclude group: 'com.google.firebase', module: 'firebase-core'
        exclude group: 'com.google.firebase', module: 'firebase-analytics'
        exclude group: 'com.google.firebase', module: 'firebase-measurement-connector'
    }
    playstoreImplementation 'com.android.installreferrer:installreferrer:2.2'
    playstoreImplementation 'com.google.gms:google-services:4.3.8'
    implementation 'org.sufficientlysecure:openpgp-api:10.0'
    implementation('com.theartofdev.edmodo:android-image-cropper:2.8.0') {
        exclude group: 'com.android.support', module: 'appcompat-v7'
        exclude group: 'com.android.support', module: 'exifinterface'
    }
    implementation 'org.bouncycastle:bcmail-jdk15on:1.64'
    implementation 'org.gnu.inet:libidn:1.15'
    implementation 'com.google.zxing:core:3.3.3' // > 3.3.x not working below SDK 24
    implementation 'de.measite.minidns:minidns-hla:0.2.4'
    implementation 'me.leolin:ShortcutBadger:1.1.22@aar'
    implementation 'org.whispersystems:signal-protocol-java:2.6.2'
    implementation 'com.makeramen:roundedimageview:2.3.0'
    implementation 'jetty:javax.servlet:5.1.12'
    implementation 'com.google.code.gson:gson:2.8.6'
    implementation 'androidx.multidex:multidex:2.0.1'
    implementation 'androidx.legacy:legacy-support-v13:1.0.0'
    implementation 'androidx.appcompat:appcompat:1.3.1'
    implementation 'androidx.exifinterface:exifinterface:1.3.3'
    implementation 'androidx.legacy:legacy-support-v4:1.0.0'
    implementation 'androidx.emoji:emoji:1.1.0'
    gitImplementation 'androidx.emoji:emoji-appcompat:1.1.0'
    gitImplementation 'androidx.emoji:emoji-bundled:1.1.0'
    implementation 'androidx.recyclerview:recyclerview:1.2.1'
    implementation 'com.google.android.material:material:1.4.0'
    implementation 'androidx.cardview:cardview:1.0.0' // for compatibility
    implementation 'com.davemorrissey.labs:subsampling-scale-image-view:3.10.0'
    implementation 'com.google.android.exoplayer:exoplayer-core:2.15.0'
    implementation 'com.google.android.exoplayer:exoplayer-ui:2.15.0'
    implementation 'com.wefika:flowlayout:0.4.1'
    implementation 'com.googlecode.ez-vcard:ez-vcard:0.10.5'
    implementation 'org.jxmpp:jxmpp-jid:1.0.2'
    implementation 'org.hsluv:hsluv:0.2'
    implementation 'org.conscrypt:conscrypt-android:2.5.2'
    implementation 'pl.droidsonroids.gif:android-gif-drawable:1.2.23'
    implementation 'me.drakeet.support:toastcompat:1.1.0'
    implementation 'org.osmdroid:osmdroid-android:6.1.11'
    implementation 'com.leinardi.android:speed-dial:3.2.0'
    implementation 'com.squareup.picasso:picasso:2.71828'
    implementation 'com.squareup.okhttp3:okhttp:4.9.2'
    implementation 'com.squareup.retrofit2:retrofit:2.9.0'
    implementation 'com.squareup.retrofit2:converter-gson:2.9.0'
    implementation 'com.google.guava:guava:31.0.1-android'
    implementation 'com.github.AppIntro:AppIntro:6.1.0'
    implementation 'androidx.browser:browser:1.3.0'
    implementation 'com.otaliastudios:transcoder:0.9.1' // 0.10.4 seems to be buggy
    implementation fileTree(include: ['libwebrtc-m92.aar'], dir: 'libs')
}

ext {
    travisBuild = System.getenv("TRAVIS") == "true"
    preDexEnabled = System.getProperty("pre-dex", "true")
}

android {

    //noinspection GradleCompatible
    compileSdkVersion 30

    defaultConfig {
        minSdkVersion 21
        targetSdkVersion 30

        //versionNameSuffix " beta_(2021-12-19)" // " beta_(XXXX-XX-XX)" // activate for beta versions
        versionCode 111
        versionName "1.5"
        //resConfigs "en"

        archivesBaseName += "-$versionName"
        archivesBaseName += "$versionNameSuffix"  // activate for beta versions
        applicationId "de.monocles.chat"
        multiDexEnabled true

        buildConfigField("String", "LOGTAG", '"monocles chat"')
        buildConfigField("String", "DOMAIN_LOCK", 'null')
        buildConfigField("boolean", "SHOW_INTRO", 'true')
        buildConfigField("String", "UPDATE_URL", '"https://monocles.de/chat/update/"')
        resValue "string", "applicationId", applicationId
        resValue "string", "app_name", "monocles chat"
    }

    dataBinding {
        enabled true
    }

    packagingOptions {
        //X86
        exclude "lib/x86/**"
        //X86_64
        exclude "lib/x86_64/**"
        //armeabi
        //exclude "lib/armeabi/**"
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }

    flavorDimensions("distribution")

    productFlavors {
        playstore {
            dimension "distribution"
            versionNameSuffix "-playstore"
            applicationId "de.monocles.chat"
            buildConfigField("boolean", "SHOW_MIGRATION_INFO", 'false')
            resValue "string", "applicationId", applicationId
        }
        git {
            dimension "distribution"
            buildConfigField("boolean", "SHOW_MIGRATION_INFO", 'true')
        }
    }
    if (project.hasProperty('mStoreFile') &&
            project.hasProperty('mStorePassword') &&
            project.hasProperty('mKeyAlias') &&
            project.hasProperty('mKeyPassword')) {
        signingConfigs {
            release {
                storeFile file(mStoreFile)
                storePassword mStorePassword
                keyAlias mKeyAlias
                keyPassword mKeyPassword
            }
        }
        buildTypes {
            release {
                debuggable false
                signingConfig = signingConfigs.release
                minifyEnabled true
                shrinkResources true
                runProguard true
                proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
            }

            debug {
                debuggable true
                buildTypes.release.signingConfig = null
                minifyEnabled true
                shrinkResources true
                runProguard true
                proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
            }

        }
    }

    lintOptions {
        error 'StringFormatInvalid' , 'StringFormatMatches'
        disable 'ExtraTranslation', 'MissingTranslation', 'InvalidPackage', 'MissingQuantity', 'AppCompatResource', 'RestrictedApi'
        checkReleaseBuilds true
        abortOnError false
    }

    subprojects {

        afterEvaluate {
            if (getPlugins().hasPlugin('android') ||
                    getPlugins().hasPlugin('android-library')) {

                configure(android.lintOptions) {
                    disable 'AndroidGradlePluginVersion', 'MissingTranslation'
                }
            }

        }
    }

    packagingOptions {
        exclude 'META-INF/BCKEY.DSA'
        exclude 'META-INF/BCKEY.SF'
    }
}
